

open import Agda.Primitive

-- NÃ£o existe na apostila
--> Inicio
  
infix 4 _â‰¡_

data _â‰¡_ {l}{A : Set l}(x : A) : A â†’ Set l where
  refl : x â‰¡ x
  
{-# BUILTIN EQUALITY _â‰¡_ #-}
{-# BUILTIN REFL refl #-}

trans : âˆ€ {l}{A : Set l}{x y z : A} â†’ x â‰¡ y â†’ y â‰¡ z â†’ x â‰¡ z
trans refl refl = refl
  
cong : âˆ€ {l}{A B : Set l}{x y : A}(f : A â†’ B) â†’ x â‰¡ y â†’ f x â‰¡ f y
cong f refl = refl

sym : âˆ€ {l}{A : Set l}{x y : A} â†’ x â‰¡ y â†’ y â‰¡ x
sym refl = refl

infixr 1 _â‰¡âŸ¨_âŸ©_

_â‰¡âŸ¨_âŸ©_ : âˆ€ {l}{A : Set l}(x : A) {y z} â†’ x â‰¡ y â†’ y â‰¡ z â†’ x â‰¡ z
x â‰¡âŸ¨ xy âŸ© yz = trans xy yz

infixr 1 _âˆŽ

_âˆŽ : âˆ€ {l}{A : Set l}(x : A) â†’ x â‰¡ x
x âˆŽ = refl

--> Fim

  --~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  
  -- Chapter 1
  --~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

module BasicFunction where

  -- 1.2.1 Day of Week
  --~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  
  data Day : Set where
    Monday    : Day
    Tuesday   : Day
    Wednesday : Day
    Thursday  : Day
    Friday    : Day
    Saturday  : Day
    Sunday    : Day

  nextDay : Day â†’ Day
  nextDay Monday    = Tuesday
  nextDay Tuesday   = Wednesday
  nextDay Wednesday = Thursday
  nextDay Thursday  = Friday
  nextDay Friday    = Saturday
  nextDay Saturday  = Sunday
  nextDay Sunday    = Monday
  --~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  
  -- 1.2.2 Booleans
  --~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  data Bool : Set where
    True  : Bool
    False : Bool

  Â¬ : Bool â†’ Bool
  Â¬ True  = False
  Â¬ False = True
  
  _âˆ§_ : Bool â†’ Bool â†’ Bool
  _âˆ§_ True t  = t
  _âˆ§_ False _ = False

  _âˆ¨_ : Bool â†’ Bool â†’ Bool
  _âˆ¨_ True _  = True
  _âˆ¨_ False t = t
  
  infix  1  Â¬
  infixl 10 _âˆ¨_
  infixl 10 _âˆ§_

  -- â‡’ Exercise 1.1 (nand)
  ------------------------------------------------------------------------------------------------------
  
  nand : Bool â†’ Bool â†’ Bool
  nand a b = Â¬ (a âˆ§ b)
  
  ------------------------------------------------------------------------------------------------------
  
  -- â‡’ Execise 1.2 (and3)
  ------------------------------------------------------------------------------------------------------

  and3 : Bool â†’ Bool â†’ Bool â†’ Bool
  and3 a b c =  a âˆ§ b âˆ§ c

  ------------------------------------------------------------------------------------------------------

open BasicFunction public

module Natural where

  -- 1.2.4 Numbers
  --~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  
  data â„• : Set where
    zero : â„•
    suc  : â„• â†’ â„•

  {-# BUILTIN NATURAL â„• #-} 
       
  infix 1 suc
  
  pred : â„• â†’ â„•
  pred zero    = zero
  pred (suc n) = n

  evenb : â„• â†’ Bool
  evenb zero          = True
  evenb (suc zero)    = False
  evenb (suc (suc n)) = evenb n
  
  --~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  
  -- â‡’ Exercise 1.3 (oddb)
  ------------------------------------------------------------------------------------------------------

  oddb : â„• â†’ Bool
  oddb zero          = False
  oddb (suc zero)    = True
  oddb (suc (suc n)) = oddb n 
  --oddb n = Â¬ (evenb n)

  ------------------------------------------------------------------------------------------------------
  
  _+_ : â„• â†’ â„• â†’ â„•
  zero  + m = m
  suc n + m = suc (n + m)

  _Ã—_ : â„• â†’ â„• â†’ â„•
  zero  Ã— m = zero
  suc n Ã— m = m + (n Ã— m)

  _-_ : â„• â†’ â„• â†’ â„• 
  zero    - _       = zero
  suc n   - zero    = suc n
  (suc n) - (suc m) = n - m 

  infixl 20 _+_
  infixl 20 _-_
  infixl 10 _Ã—_

  --~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  
  -- â‡’ Exercise 1.4 (fatorial)
  ------------------------------------------------------------------------------------------------------

  fatorial : â„• â†’ â„•
  fatorial zero = 1
  fatorial (suc n) = (suc n) Ã— fatorial n

  --_! : â„• â†’ â„• 
  --zero ! = 1
  --(suc n) ! = (suc n) Ã— (n !)  
  
  beqNat : â„• â†’ â„• â†’ Bool
  beqNat zero zero       = True
  beqNat (suc _) zero    = False
  beqNat zero (suc _)    = False
  beqNat (suc n) (suc m) = beqNat n m

  bleNat : â„• â†’ â„• â†’ Bool
  bleNat zero    _       = True
  bleNat (suc _) zero    = False
  bleNat (suc n) (suc m) = bleNat n m

  -- â‡’ Exercise 1.5 (bltNat)
  ------------------------------------------------------------------------------------------------------

  bltNat : â„• â†’ â„• â†’ Bool
  bltNat _       zero    = True
  bltNat zero    (suc _) = False
  bltNat (suc n) (suc m) = bltNat n m

  ------------------------------------------------------------------------------------------------------
  
  
  -- 1.3  Proof by Simplification
  --~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  
  lemmaPlus0N : âˆ€ (n : â„•) â†’ 0 + n â‰¡ n
  lemmaPlus0N n = refl
  
  lemmaPlus1N : âˆ€ (n : â„•) â†’ 1 + n â‰¡ suc n
  lemmaPlus1N n = refl
  
  lemmaMult0L : âˆ€ (n : â„•) â†’ 0 Ã— n â‰¡ 0
  lemmaMult0L n = refl
  
  --~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  
  -- 1.4  Proof by Rewriting
  --~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  
  plusIdExample : âˆ€ (n m : â„•) â†’ n â‰¡ m â†’ n + n â‰¡ m + m
  plusIdExample n .n refl = refl

  -- â‡’ Exercise 1.6 (plusIdExercise)
  ------------------------------------------------------------------------------------------------------

  plusIdExercice : âˆ€ (n m o : â„•) â†’ n â‰¡ m â†’ m â‰¡ o â†’ n + m â‰¡ m + o
  plusIdExercice n .n .n refl refl = refl

  ------------------------------------------------------------------------------------------------------  

  mult0Plus : âˆ€ (n m : â„•) â†’ (0 + n) Ã— m â‰¡ n Ã— m
  mult0Plus n m = refl

  --~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  
  -- 1.5  Proof by Case Analysis
  --~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

  beqNatN+1=0 : âˆ€ (n : â„•) â†’ beqNat (n + 1) 0 â‰¡ False
  beqNatN+1=0 zero    = refl
  beqNatN+1=0 (suc n) = refl

  notInvolutive : âˆ€ (b : Bool) â†’ Â¬ (Â¬ b) â‰¡ b
  notInvolutive False = refl
  notInvolutive True  = refl

  zeroNBeq+1 : âˆ€ (n : â„•) â†’ beqNat 0 (n + 1) â‰¡ False
  zeroNBeq+1 zero    = refl
  zeroNBeq+1 (suc n) = refl

  --~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  
  -- 1.6  More Exercises
  --~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  
  -- â‡’ Exercise 1.8 (identityFnAppliedTwice and negativeFnAppliedTwice)
  ------------------------------------------------------------------------------------------------------
  
  identityFnAppliedTwice : âˆ€ (f : Bool â†’ Bool)(_ : âˆ€ (x : Bool) â†’ f x â‰¡ x) â†’ âˆ€ (b : Bool) â†’ f (f b) â‰¡ b -- Alterar no GitHub
  identityFnAppliedTwice f y x = trans (y (f x)) (y x) -- Resolveu sozinho

  negativeFnAppliedTwice : âˆ€ (f : Bool â†’ Bool)(_ : âˆ€ (x : Bool) â†’ f x â‰¡ Â¬ x) â†’ âˆ€ (b : Bool) â†’ f (f b) â‰¡ b
  negativeFnAppliedTwice f y x = trans (y (f x)) (trans (cong Â¬ (y x)) (notInvolutive x))

  ------------------------------------------------------------------------------------------------------

  -- â‡’ Exercise 1.9 (lemma)
  ------------------------------------------------------------------------------------------------------

  lemma : âˆ€ (b c : Bool) â†’ (b âˆ§ c) â‰¡ (b âˆ¨ c) â†’ b â‰¡ c
  lemma False c prf = prf
  lemma True  c prf = sym prf
  
  -----------------------------------------------------------------------------------------------------
    
  -- â‡’ Exercise 1.10 
  -----------------------------------------------------------------------------------------------------
  
  -- 1.10 - 1 (defition of bynary number)
  -----------------------------------------------------------------------------------------------------
  
  data ð”¹ : Set where -- binary number
    zero   : ð”¹       -- zero
    double : ð”¹ â†’ ð”¹   -- par
    dPlus  : ð”¹ â†’ ð”¹   -- impar

  -----------------------------------------------------------------------------------------------------

  -- 1.10 - 2 (sucessor for bynary number âˆ§ convertion [bynary number â†’ unary number])
  -----------------------------------------------------------------------------------------------------
  sucð”¹ : ð”¹ â†’ ð”¹
  sucð”¹ zero       = dPlus zero
  sucð”¹ (double x) = dPlus x
  sucð”¹ (dPlus x)  = double (sucð”¹ x)

  ð”¹â„• : ð”¹ â†’ â„•
  ð”¹â„• zero       = zero
  ð”¹â„• (double x) = 2 Ã— (ð”¹â„• x)
  ð”¹â„• (dPlus x)  = 2 Ã— (ð”¹â„• x) + 1
  ------------------------------------------------------------------------------------------------------

open Natural public

  --~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  
  -- Chapter 2
  --~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

module Induction where
  
  --~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  
  -- 2.1  Naming Case
  --~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  
  andElim1 : âˆ€ (b c : Bool) â†’ b âˆ§ c â‰¡ True â†’ b â‰¡ True
  andElim1 True  c prf = refl
  andElim1 False c ()

  -- â‡’ Exercise 2.1 (andElim2)
  ------------------------------------------------------------------------------------------------------

  andElim2 : âˆ€ (b c : Bool) â†’ b âˆ§ c â‰¡ True â†’ c â‰¡ True
  andElim2 b True  prf = refl
  --andElim2 b False ()
  andElim2 True False ()
  andElim2 False False ()

  ------------------------------------------------------------------------------------------------------

  --~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

  -- 2.2 Proof by Induction
  --~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

  plus0R : âˆ€ (n : â„•) â†’ n + 0 â‰¡ n
  plus0R zero    = refl
  plus0R (suc n) = cong suc (plus0R n)

  minusDiag : âˆ€ (n : â„•) â†’ n - n â‰¡ 0
  minusDiag zero    = refl
  minusDiag (suc n) = minusDiag n

  -- â‡’ Exercise 2.2 (mult0R âˆ§ plusNSucM âˆ§ plusComm âˆ§ plusAssoc)
  ------------------------------------------------------------------------------------------------------

  mult0R : âˆ€ (n : â„•) â†’ n Ã— 0 â‰¡ 0
  mult0R zero    = refl
  mult0R (suc n) = mult0R n

  plusNSucM : âˆ€ (n m : â„•) â†’ suc (n + m) â‰¡ n + suc m
  plusNSucM zero    m = refl 
  plusNSucM (suc n) m = cong suc (plusNSucM n m)

  plusComm : âˆ€ (n m : â„•) â†’ n + m â‰¡ m + n
  plusComm zero    m = sym (plus0R m)
  plusComm (suc n) m = trans (cong suc (plusComm n m)) (plusNSucM m n)

  plusAssoc : âˆ€ (n m p : â„•) â†’ n + (m + p) â‰¡ (n + m) + p
  plusAssoc zero    m p = refl
  plusAssoc (suc n) m p = cong suc (plusAssoc n m p)
  ------------------------------------------------------------------------------------------------------

  -- â‡’ Exercise 2.3 (doublePlus)
  ------------------------------------------------------------------------------------------------------

  double1 : â„• â†’ â„• -- jÃ¡ existe double (criado para o binÃ¡rio)
  double1 zero    = zero
  double1 (suc n) = suc (suc (double1 n))
  
  doublePlus : âˆ€ (n : â„•) â†’ double1 n â‰¡ n + n
  doublePlus zero    = refl
  doublePlus (suc n) = cong suc (trans (cong suc (doublePlus n)) (plusNSucM n n))
  
  ------------------------------------------------------------------------------------------------------
  
  --~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  
  -- 2.3  Proofs within Proofs
  --~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

  mult0Plus1 : âˆ€ (n m : â„•) â†’ (0 + n) Ã— m â‰¡  n Ã— m
  mult0Plus1 n m = cong (Î» n â†’ n Ã— m) h where
    h : 0 + n â‰¡ n
    h = refl

  plusRearrenge : âˆ€ (n m p q : â„•) â†’ (n + m) + (p + q) â‰¡ (m + n) + (p + q)
  plusRearrenge n m p q = cong (\n â†’ n + (p + q)) nmComm where
    nmComm : n + m â‰¡ m + n
    nmComm = plusComm n m

  -- â‡’ Exercise 2.4 (plusSwap âˆ§ multComm)
  ------------------------------------------------------------------------------------------------------

  plusSwap : âˆ€ (n m p : â„•) â†’ n + (m + p) â‰¡ m + (n + p)
  plusSwap n m p = trans (plusAssoc n m p) (trans (cong (Î» a â†’ a + p) nmComm) (mnpComm m n p) )  where
    nmComm : n + m â‰¡ m + n 
    nmComm = plusComm n m
    mnpComm : âˆ€ ( m n p : â„•) â†’ m + n + p â‰¡ m + (n + p)
    mnpComm zero    n p = refl
    mnpComm (suc m) n p = cong suc (mnpComm m n p)

  multComm : âˆ€ (n m : â„•) â†’ n Ã— m â‰¡ m Ã— n
  multComm zero    m = sym (mult0R m)
  multComm (suc n) m = trans (cong (Î» a â†’ m + a) (multComm n m)) (mMxN m n) where
    mMxN : âˆ€ (m n : â„•) â†’ m + (m Ã— n) â‰¡ m Ã— (suc n)
    mMxN zero    n = refl
    mMxN (suc m) n = cong suc (trans (plusSwap m n (m Ã— n)) (cong (Î» a â†’ n + a) (mMxN m n))) 
  ------------------------------------------------------------------------------------------------------

  -- â‡’ Exercise 2.5 (evenNoddSucN) -- Corrigir na Apostila
  ------------------------------------------------------------------------------------------------------

  --evenNoddSucN : âˆ€ (n : â„•) â†’ evenb n â‰¡ Â¬ (oddb (suc n)) -- n e par (se e somente se) nao (impar (suc n))? Falso ...
  --evenNoddSucN zero    = {!!}
  --evenNoddSucN (suc n) = {!!}

  evenOddSucN : âˆ€ (n : â„•) â†’ evenb n â‰¡ oddb (suc n)
  evenOddSucN zero       = refl
  evenOddSucN (suc zero) = refl
  evenOddSucN (suc (suc n)) = evenOddSucN n


  ------------------------------------------------------------------------------------------------------

  --~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  
  -- 2.4 More Exercices
  --~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  
  -- â‡’ Exercise 2.6 (bleNatrefl âˆ§ zeroNbeqSuc âˆ§ andFalseR âˆ§ plusBleCompatL âˆ§ sucNBeq0 âˆ§ mult1L âˆ§ all3Spec âˆ§ multPlusDistR âˆ§ multAssoc âˆ§ beqNatRefl)
  ------------------------------------------------------------------------------------------------------
  
  bleNatRefl : âˆ€ (n : â„•) â†’ True â‰¡ bleNat n n
  bleNatRefl zero    = refl
  bleNatRefl (suc n) = bleNatRefl n

  zeroNbeqSuc : âˆ€ (n : â„•) â†’ beqNat 0 (suc n) â‰¡ False
  zeroNbeqSuc n = refl

  andFalseR : âˆ€ (b : Bool) â†’ b âˆ§ False â‰¡ False
  andFalseR True  = refl
  andFalseR False = refl

  plusBleCompatL : âˆ€ (n m p : â„•) â†’ bleNat n m â‰¡ True â†’ bleNat (p + n) (p + m) â‰¡ True
  plusBleCompatL n m zero    prf = prf
  plusBleCompatL n m (suc p) prf = plusBleCompatL n m p prf

  sucNBeq0 : âˆ€ (n : â„•) â†’ beqNat (suc n) zero â‰¡ False
  sucNBeq0 n = refl

  mult1L : âˆ€ (n : â„•) â†’ 1 Ã— n â‰¡ n
  mult1L n = plus0R n

  all3Spec : âˆ€ (b c : Bool) â†’ (b âˆ§ c) âˆ¨ (Â¬ b âˆ¨ Â¬ c) â‰¡ True
  all3Spec True True  = refl
  all3Spec True False = refl
  all3Spec False c    = refl

  multPlusDistrR : âˆ€ (n m p : â„•) â†’ (n + m) Ã— p â‰¡ (n Ã— p) + (m Ã— p)
  multPlusDistrR n m zero rewrite mult0R (n + m) | mult0R n | mult0R m =  refl
  multPlusDistrR n m (suc p) rewrite multComm n (suc p) | multComm m (suc p) = trans (multComm (n + m) (suc p)) {!!} 

  multAssoc : âˆ€ (n m p : â„•) â†’ n Ã— (m Ã— p) â‰¡ (n Ã— m) Ã— p
  multAssoc zero    m p = refl
  multAssoc (suc n) zero p = multAssoc n zero p
  multAssoc (suc n) (suc m) p = {!!}

  beqNatRefl : âˆ€ (n : â„•) â†’ True â‰¡ beqNat n n
  beqNatRefl zero    = refl
  beqNatRefl (suc n) = beqNatRefl n

open Induction public

  --~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  
  -- Chapter 3
  --~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

module Lists where

  data â„•Prod : Set where
    _,_ : â„• â†’ â„• â†’ â„•Prod

  fst : â„•Prod â†’ â„• -- colocar espaco
  fst (n , _) = n

  snd : â„•Prod â†’ â„•
  snd (_ , n) = n

  surjectivePairing : âˆ€ (n m : â„•) â†’ (n , m) â‰¡ ((fst (n , m)) , (snd (n , m)))
  surjectivePairing n m = refl
  
  surjectivePairing1 : âˆ€ (p : â„•Prod) â†’ p â‰¡ ((fst p) , (snd p))
  surjectivePairing1 (n , m) = refl

  -- Exercise 3.1
  ------------------------------------------------------------------------------------------------------

  swap : â„•Prod â†’ â„•Prod
  swap (n , m) = (m , n)

  fstSndSwap : (p : â„•Prod) â†’ (snd p , fst p) â‰¡ swap p
  fstSndSwap (n , m) = refl

  ------------------------------------------------------------------------------------------------------
  
  --~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  
  -- 3.2 List of Numbers
  --~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

  data NList : Set where
    nil : NList
    _,_ : â„• â†’ NList â†’ NList

  infixr 4 _,_

  sample : NList
  sample = 1 , 2 , 3 , nil

  repeat : â„• â†’ â„• â†’ NList
  repeat n zero    = nil
  repeat n (suc m) = n , repeat n m
  
  length : NList â†’ â„•
  length nil = 0
  length (x , xs) = suc (length xs)

  infixr 4 _++_
  
  _++_ : NList â†’ NList â†’ NList
  nil      ++ ys = ys
  (x , xs) ++ ys = x , (xs ++ ys)

  head : NList â†’ (default : â„•) â†’ â„•
  head nil     d = d
  head (x , _) _ = x

  tail : NList â†’ NList
  tail nil      = nil
  tail (x , xs) = xs

  -- Exercise 3.2
  ------------------------------------------------------------------------------------------------------
  
  nonZeros : NList â†’ NList
  nonZeros nil         = nil
  nonZeros (zero , xs) = nonZeros xs
  nonZeros (x , xs)    = x , nonZeros xs

  testNonZeros : nonZeros ( 0 , 1 , 0 , 2 , 0 , nil) â‰¡ (1 , 2 , nil)
  testNonZeros = refl
 
  ------------------------------------------------------------------------------------------------------
  
  -- Exercise 3.3
  ------------------------------------------------------------------------------------------------------

  --if_than_else_ : âˆ€ {A : Set} â†’ Bool â†’ A â†’ A â†’ A
  --if True  than a else _ = a
  --if False than _ else a = a
 
  oddMembers : NList â†’ NList
  oddMembers nil      = nil
  oddMembers (n , xs) with oddb n
  ...| True  = n , oddMembers xs
  ...| False = oddMembers xs
  --oddMembers (n , xs) = if (oddb n) than (n , oddMembers xs) else oddMembers xs
  
  
  testOddMembers : oddMembers (1 , 2 , 3 , 4 , nil) â‰¡ (1 , 3 , nil)
  testOddMembers = refl

  ------------------------------------------------------------------------------------------------------

  -- Exercise 3.4
  ------------------------------------------------------------------------------------------------------
 
  alternative : NList â†’ NList â†’ NList
  alternative nil      ys = ys
  alternative (x , xs) ys = x , alternative ys xs

  ------------------------------------------------------------------------------------------------------

  -- 3.3  Bag via Lists
  ------------------------------------------------------------------------------------------------------

  Bag : Set
  Bag = NList

  -- â‡’ Exercise 3.5
  ---

  count : â„• â†’ NList â†’ â„•
  count _ nil = zero
  count x (y , ys) with (beqNat x y)
  ...| True  = suc (count x ys)
  ...| False = count x ys 
  --count x (y , ys) = if (x â‰¡ y) than suc (count x ys) else (count x ys)
  
  testCount1 : count 1 (1 , 2 , 1 , nil) â‰¡ 2
  testCount1 = refl
  
  testCount2 : count 3 (1 , 2 , 1 , nil) â‰¡ 0
  testCount2 = refl
  
  sum : Bag â†’ â„•
  sum nil = zero
  sum (x , xs) = x + (sum xs)
  
  testSum : sum (1 , 2 , 1 , nil) â‰¡ 4
  testSum = refl

  add : â„• â†’ Bag â†’ Bag
  add x ys = (x , ys)

  testAdd : count 1 (add 1 (1 , 2 , 1 , nil)) â‰¡ 3
  testAdd = refl

  member : â„• â†’ Bag â†’ Bool
  member _ nil = False
  member x (y , ys) with (beqNat x y)
  ...| True  = True
  ...| False = member x ys
  
  testMember1 : member 1 (1 , 2 , 1 , nil) â‰¡ True
  testMember1 = refl

  testMember2 : member 3 (1 , 2 , 1 , nil) â‰¡ False
  testMember2 = refl

  -- â‡’ Exercise 3.6
  ----

  removeOne : â„• â†’ Bag â†’ Bag
  removeOne _ nil = nil
  removeOne x (y , ys) with (beqNat x y)
  ...| True  = ys
  ...| False = y , removeOne x ys

  testRemoveOne1 : count 1 (removeOne 1 (1 , 2 , 1 , nil)) â‰¡ 1 
  testRemoveOne1 = refl

  testRemoveOne2 : count 1 (removeOne 2 (1 , 2 , 1 , nil)) â‰¡ 2 
  testRemoveOne2 = refl

  removeAll : â„• â†’ Bag â†’ Bag
  removeAll _ nil = nil
  removeAll x (y , ys) with (beqNat x y)
  ...| True  = removeAll x ys
  ...| False = y , removeAll x ys

  testRemoveAll : count 1 (removeAll 1 (1 , 2 , 1 , nil)) â‰¡ 0
  testRemoveAll = refl

  contem : â„• â†’ Bag â†’ Bool
  contem _ nil = False
  contem x (y , ys) with (beqNat x y)
  ...| True  = True
  ...| False = contem x ys

  subset : Bag â†’ Bag â†’ Bool
  subset nil _   = True
  subset (x , xs) ys = (contem x ys) âˆ§ subset xs ys

  testSubset1 : subset (1 , 2 , nil) (1 , 2 , 1 , nil) â‰¡ True
  testSubset1 = refl
  
  testSubset2 : subset (2 , 3 , nil) (1 , 2 , 4 , nil) â‰¡ False
  testSubset2 = refl

  testSubsetA : subset (2 , 3 , nil) ( 3 , 2 , 1 , nil) â‰¡ True
  testSubsetA = refl
  
  testSubset3 : subset (1 , 1 , nil) (1 , 2 , 1 , nil) â‰¡ True
  testSubset3 = refl

  ------------------------------------------------------------------------------------------------------
  
  -- â‡’ Exercise 3.7 --> Nao entendi o que tem que fazer
  ------------------------------------------------------------------------------------------------------

  -- 3.4  Reasoning about Lists
  ------------------------------------------------------------------------------------------------------
  
  nilAppL : âˆ€ (n : NList) â†’ nil ++ n â‰¡ n
  nilAppL n = refl

  tailLengthPred : âˆ€ (n : NList) â†’ pred (length n) â‰¡ length (tail n)
  tailLengthPred nil     = refl
  tailLengthPred (x , n) = refl
  
  appAssoc : âˆ€ (l1 l2 l3 : NList) â†’ l1 ++ (l2 ++ l3) â‰¡ (l1 ++ l2) ++ l3
  appAssoc nil      l2 l3 = refl
  appAssoc (x , l1) l2 l3 =                                              --trans refl (cong (Î» p â†’ x , p) (appAssoc l1 l2 l3)) 
    ((x , l1) ++ l2 ++ l3)   â‰¡âŸ¨ refl âŸ©
    (x , (l1 ++ (l2 ++ l3))) â‰¡âŸ¨ cong (Î» p â†’ x , p) (appAssoc l1 l2 l3) âŸ© 
    (x , ((l1 ++ l2) ++ l3))
    âˆŽ

  appLength : âˆ€ (n m : NList) â†’ length (n ++ m) â‰¡ length n + length m
  appLength nil      m = refl
  appLength (x , xs) m =                                                 --cong suc (appLength xs m)
    length ((x , xs) ++ m)     â‰¡âŸ¨ refl âŸ© 
    length (x , (xs ++ m))     â‰¡âŸ¨ refl âŸ©
    suc (length (xs ++ m))     â‰¡âŸ¨ cong suc (appLength xs m) âŸ©
    suc (length xs + length m) â‰¡âŸ¨ refl âŸ© 
    length (x , xs) + length m
    âˆŽ 
    

  -- â‡’ Exercise 3.8
  ------------------------------------------------------------------------------------------------------
  
  snoc : â„• â†’ NList â†’ NList
  snoc n nil      = n , nil
  snoc n (x , xs) = x , snoc n xs

  rev : NList â†’ NList
  rev nil      = nil
  rev (x , xs) = snoc x (rev xs)

  lengthSnoc : âˆ€ (n : â„•)(l : NList) â†’ length (snoc n l) â‰¡ suc (length l)
  lengthSnoc n nil      = refl
  lengthSnoc n (x , xs) = 
    length (snoc n (x , xs)) â‰¡âŸ¨ refl âŸ©
    length (x , (snoc n xs)) â‰¡âŸ¨ refl âŸ©
    suc (length (snoc n xs)) â‰¡âŸ¨ cong suc (lengthSnoc n xs) âŸ©
    suc (length (x , xs))
    âˆŽ

  revLength : âˆ€ (n : NList) â†’ length (rev n) â‰¡ length n
  revLength nil      = refl
  revLength (x , xs) = 
    length (rev (x , xs))    â‰¡âŸ¨ refl âŸ©
    length (snoc x (rev xs)) â‰¡âŸ¨ lengthSnoc x (rev xs) âŸ©
    suc (length (rev xs))    â‰¡âŸ¨ cong suc (revLength xs) âŸ©
    suc (length xs)          â‰¡âŸ¨ refl âŸ© 
    length (x , xs) 
    âˆŽ

  -- 3.4.3  List Exercise, part 1

  appNilEnd : âˆ€ (l : NList) â†’ l ++ nil â‰¡ l
  appNilEnd nil     = refl
  appNilEnd (x , l) = 
    (x , l) ++ nil   â‰¡âŸ¨ refl âŸ©
    (x , (l ++ nil)) â‰¡âŸ¨ cong (Î» a â†’ x , a ) (appNilEnd l) âŸ©
    (x , l)
    âˆŽ

  appAss : âˆ€ (l1 l2 l3 l4 : NList) â†’ l1 ++ (l2 ++ (l3 ++ l4)) â‰¡ ((l1 ++ l2) ++ l3) ++ l4
  appAss nil l2 l3 l4 = appAssoc l2 l3 l4
  appAss (x , l1) l2 l3 l4 = 
    (x , l1) ++ (l2 ++ (l3 ++ l4)) â‰¡âŸ¨ refl âŸ©
    (x , l1 ++ (l2 ++ (l3 ++ l4))) â‰¡âŸ¨ cong (Î» a â†’ x , a) (appAss l1 l2 l3 l4) âŸ©
    (x , ((l1 ++ l2) ++ l3) ++ l4)
    âˆŽ

  snocApp : âˆ€ (n : â„•)(l : NList) â†’ snoc n l â‰¡ l ++ (n , nil) 
  snocApp n nil     = refl
  snocApp n (x , l) = 
    snoc n (x , l)     â‰¡âŸ¨ refl âŸ© 
    (x , snoc n l)     â‰¡âŸ¨ cong (Î» a â†’ x , a) (snocApp n l) âŸ© 
    (x , l ++ n , nil)
    âˆŽ

  distRev : âˆ€ (l1 l2 : NList) â†’ rev (l1 ++ l2) â‰¡ (rev l2) ++ (rev l1)
  distRev nil      l2 = sym (appNilEnd (rev l2))
  distRev (x , l1) l2 = 
    rev ((x , l1) ++ l2)            â‰¡âŸ¨ refl âŸ©
    snoc x (rev (l1 ++ l2))         â‰¡âŸ¨ snocApp x (rev (l1 ++ l2)) âŸ© 
    (rev (l1 ++ l2) ++ x , nil)     â‰¡âŸ¨ cong (Î» a â†’ (a ++ x , nil)) (distRev l1 l2) âŸ©
    ((rev l2 ++ rev l1) ++ x , nil) â‰¡âŸ¨ sym (appAssoc (rev l2) (rev l1) (x , nil)) âŸ© 
    rev l2 ++ rev l1 ++ x , nil     â‰¡âŸ¨ cong (Î» a â†’ rev l2 ++ a) (sym (snocApp x (rev l1))) âŸ©
    rev l2 ++ snoc x (rev l1)
    âˆŽ

  revInjective : âˆ€ (l1 l2 : NList) â†’ rev l1 â‰¡ rev l2 â†’ l1 â‰¡ l2
  revInjective nil      nil      refl = refl
  revInjective _        nil      ()
  revInjective nil      _        ()
  revInjective (x , l1) (y , l2) prf with beqNat x y
  ...| True  = {!!}
  ...| False = {!!}

  revInvolutive : âˆ€ (l : NList) â†’ rev (rev l) â‰¡ l
  revInvolutive nil     = refl
  revInvolutive (x , l) =
    rev (rev (x , l))            â‰¡âŸ¨ refl âŸ©
    rev (snoc x (rev l))         â‰¡âŸ¨ cong (Î» a â†’ rev a) (snocApp x (rev l)) âŸ©
    rev (rev l ++ x , nil)       â‰¡âŸ¨ distRev (rev l) (x , nil) âŸ© 
    rev (x , nil) ++ rev (rev l) â‰¡âŸ¨ refl âŸ©
    (x , nil) ++ rev (rev l)     â‰¡âŸ¨ refl âŸ©
    (x , rev (rev l))            â‰¡âŸ¨ cong (Î» a â†’ x , a) (revInvolutive l) âŸ© 
    (x , l) 
    âˆŽ
 
  ------------------------------------------------------------------------------------------------------
  
  --3.5  Maybe
  ------------------------------------------------------------------------------------------------------
  
  data NMaybe : Set where
    Just    : â„• â†’ NMaybe
    Nothing : NMaybe

  indexBad : â„• â†’ NList â†’ â„•
  indexBad _       nil      = 42 --arbitrary
  indexBad zero    (x , xs) = x
  indexBad (suc n) (x , xs) = indexBad n xs

  index : â„• â†’ NList â†’ NMaybe
  index _       nil      = Nothing
  index zero    (x , xs) = Just x
  index (suc n) (x , xs) = index n xs

  headM : NList â†’ NMaybe
  headM nil      = Nothing
  headM (x , xs) = Just x

  beqNList : NList â†’ NList â†’ Bool
  beqNList nil      nil      = True
  beqNList _        nil      = False
  beqNList nil      _        = False
  beqNList (x , xs) (y , ys) with beqNat x y
  ...| True  = beqNList xs ys
  ...| False = False

  beqNListRefl : âˆ€ (l : NList) â†’ beqNList l l â‰¡ True
  beqNListRefl nil     = refl
  beqNListRefl (x , l) with beqNat x x
  ...| True  = beqNListRefl l 
  ...| False = {!!}

--open Lists public

module Polymorphism where

  data BList : Set where
    nil  : BList
    cons : Bool â†’ BList â†’ BList

  data List {l}(A : Set l) : Set l where
    nil : List A
    _,_ : A â†’ List A â†’ List A

  lengthA : (A : Set) â†’ List A â†’ â„•
  lengthA A nil      = zero
  lengthA A (_ , xs) = suc (lengthA A xs)

  testLengthBool : lengthA Bool (nil) â‰¡ 0
  testLengthBool = refl

  test1LengthBool : lengthA â„• (zero , nil) â‰¡ 1
  test1LengthBool = refl

  test3LengthBool : lengthA Bool (True , nil) â‰¡ 1
  test3LengthBool = refl

  test4LengthBool : lengthA Bool (False , (True , nil)) â‰¡ 2 -- Olhar com o Rodrigo
  test4LengthBool = refl

  _âŸ¨+âŸ©_ : (A : Set) â†’ List A â†’ List A â†’ List A
  _âŸ¨+âŸ©_ A nil      ys = ys
  _âŸ¨+âŸ©_ A (x , xs) ys = x , (_âŸ¨+âŸ©_ A xs ys)

  snocL : (A : Set) â†’ A â†’ List A â†’ List A
  snocL A x nil      = x , nil
  snocL A x (y , ys) = y , (snocL A x ys)

  revL : (A : Set) â†’ List A â†’ List A
  revL A nil      = nil
  revL A (x , xs) = snocL A x (revL A xs) 

  -- 
  Age = â„•
  
  --appL A nil      ys = ys
  --appL A (x , xs) ys = x , (appL A xs ys)

  --

  length : {A : Set} â†’ List A â†’ â„•
  length {A} nil      = zero
  length {A} (_ , xs) = suc (length {A} xs)

  -- 4.1.5  Exercices: Polymorphic List

  repeat : {A : Set}(n : A)(count : â„•) â†’ List A
  repeat {A} _ zero        = nil
  repeat {A} n (suc count) = n , (repeat {A} n count)

  testRepeat : repeat True 2 â‰¡ True , (True , nil)
  testRepeat = refl
 
  _âŸ¨++âŸ©_ : {A : Set} â†’ List A â†’ List A â†’ List A
  nil      âŸ¨++âŸ© ys = ys
  (x , xs) âŸ¨++âŸ© ys = x , ( xs âŸ¨++âŸ© ys)

  snoc : {A : Set} â†’ A â†’ List A â†’ List A
  snoc x nil      = x , nil
  snoc x (y , ys) = y , (snoc x ys)

  rev : {A : Set} â†’ List A â†’ List A
  rev nil      = nil
  rev (x , xs) = snoc x (rev xs) 

  appAssoc : âˆ€ {A : Set}(l1 l2 l3 : List A) â†’ l1 âŸ¨++âŸ© (l2 âŸ¨++âŸ© l3) â‰¡ (l1 âŸ¨++âŸ© l2) âŸ¨++âŸ© l3
  appAssoc nil      l2 l3 = refl
  appAssoc (x , l1) l2 l3 = 
    (x , l1) âŸ¨++âŸ© (l2 âŸ¨++âŸ© l3) â‰¡âŸ¨ refl âŸ©
    x , (l1 âŸ¨++âŸ© (l2 âŸ¨++âŸ© l3)) â‰¡âŸ¨ cong (Î» a â†’ x , a) (appAssoc l1 l2 l3) âŸ©
    x , ((l1 âŸ¨++âŸ© l2) âŸ¨++âŸ© l3) 
    âˆŽ

  nilApp : âˆ€ {A : Set}(l : List A) â†’ nil âŸ¨++âŸ© l â‰¡ l -- Olhar com o Rodrigo
  nilApp {A} l = refl
  
  nilApp2 : âˆ€ {A : Set}(l : List A) â†’ l âŸ¨++âŸ© nil â‰¡ l -- Olhar com o Rodrigo
  nilApp2 nil     = refl
  nilApp2 (x , l) = 
    (x , l) âŸ¨++âŸ© nil â‰¡âŸ¨ refl âŸ©
    x ,(l âŸ¨++âŸ© nil)  â‰¡âŸ¨ cong (Î» a â†’ x , a) (nilApp2 l) âŸ© 
    x , l
    âˆŽ

  revSnoc : âˆ€ {A : Set}(v : A)(s : List A) â†’ rev (snoc v s) â‰¡ v , (rev s)
  revSnoc v nil     = refl
  revSnoc v (x , s) = 
    rev (snoc v (x , s))    â‰¡âŸ¨ refl âŸ©
    snoc x (rev (snoc v s)) â‰¡âŸ¨ cong (Î» a â†’ snoc x a) (revSnoc v s) âŸ© 
    snoc x (v , rev s)      â‰¡âŸ¨ refl âŸ©
    v , snoc x (rev s)      â‰¡âŸ¨ refl âŸ©
    v , (rev (x , s))
    âˆŽ
 
  snocApp : âˆ€ {A : Set}(n : A)(l : List A) â†’ snoc n l â‰¡ l âŸ¨++âŸ© (n , nil)
  snocApp n nil     = refl
  snocApp n (x , l) = 
    snoc n (x , l)        â‰¡âŸ¨ refl âŸ©
    x , snoc n l          â‰¡âŸ¨ cong (Î» a â†’ x , a) (snocApp n l) âŸ©
    x , (l âŸ¨++âŸ© (n , nil)) â‰¡âŸ¨ refl âŸ© 
    (x , l) âŸ¨++âŸ© (n , nil)
    âˆŽ

  distRev : âˆ€ {A : Set}(l1 l2 : List A) â†’ rev (l1 âŸ¨++âŸ© l2) â‰¡ (rev l2) âŸ¨++âŸ© (rev l1)
  distRev nil      l2 = sym (nilApp2 (rev l2))
  distRev (x , l1) l2 = 
    rev ((x , l1) âŸ¨++âŸ© l2)             â‰¡âŸ¨ refl âŸ©
    snoc x (rev (l1 âŸ¨++âŸ© l2))          â‰¡âŸ¨ cong (Î» a â†’ snoc x a) (distRev l1 l2) âŸ©
    snoc x (rev l2 âŸ¨++âŸ© rev l1)        â‰¡âŸ¨ snocApp x (rev l2 âŸ¨++âŸ© rev l1) âŸ©
    (rev l2 âŸ¨++âŸ© rev l1) âŸ¨++âŸ© (x , nil) â‰¡âŸ¨ sym (appAssoc (rev l2) (rev l1) (x , nil)) âŸ© 
    rev l2 âŸ¨++âŸ© (rev l1 âŸ¨++âŸ© (x , nil)) â‰¡âŸ¨ cong (Î» a â†’ rev l2 âŸ¨++âŸ© a) (sym (snocApp x (rev l1))) âŸ©
    rev l2 âŸ¨++âŸ© rev (x , l1)
    âˆŽ

  revInvolutive : âˆ€ {A : Set}(l : List A) â†’ rev (rev l) â‰¡ l
  revInvolutive nil     = refl
  revInvolutive (x , l) = 
    rev (rev (x , l))             â‰¡âŸ¨ refl âŸ©
    rev (snoc x (rev l))          â‰¡âŸ¨ cong (Î» a â†’ rev a) (snocApp x (rev l)) âŸ©
    rev (rev l âŸ¨++âŸ© (x , nil))     â‰¡âŸ¨ distRev (rev l) (x , nil) âŸ©
    rev (x , nil) âŸ¨++âŸ© rev (rev l) â‰¡âŸ¨ refl âŸ©
    (x , nil) âŸ¨++âŸ© rev (rev l)     â‰¡âŸ¨ refl âŸ©
    x , rev (rev l)               â‰¡âŸ¨ cong (Î» a â†’ x , a) (revInvolutive l) âŸ©
    x , l
    âˆŽ

  snocWithApp : {A : Set}(x : A)(l1 l2 : List A) â†’ snoc x (l1 âŸ¨++âŸ© l2) â‰¡ l1 âŸ¨++âŸ© (snoc x l2)
  snocWithApp x nil      l2 = refl
  snocWithApp x (y , l1) l2 = 
    snoc x ((y , l1) âŸ¨++âŸ© l2) â‰¡âŸ¨ refl âŸ©
    y , snoc x (l1 âŸ¨++âŸ© l2)   â‰¡âŸ¨ cong (Î» a â†’ y , a) (snocWithApp x l1 l2) âŸ©
    y , (l1 âŸ¨++âŸ© snoc x l2)
    âˆŽ 
 

  -- 4.1.6  Polymorphic Pairs

  data _X_ (A B : Set) : Set where
    _,_ : A â†’ B â†’ A X B

  fst : {A B : Set} â†’ A X B â†’ A
  fst (x , _) = x
  
  snd : {A B : Set} â†’ A X B â†’ B
  snd (_ , x) = x

  zip : {A B : Set} â†’ List A â†’ List B â†’ List (A X B)
  zip nil      _        = nil
  zip _        nil      = nil
  zip (x , xs) (y , ys) = (x , y) , zip xs ys

  -- Exercise 4.1

  unzip : {A B : Set} â†’ List (A X B) â†’ (List A X List B)
  unzip nil     = nil , nil
  unzip ((x , y) , a) = (x , fst z) , (y , snd z) where
    z = unzip a

  -- 4.1.7  Polymorphic Maybe

  data Maybe (A : Set) : Set where
    Just    : A â†’ Maybe A
    Nothing : Maybe A

  index : {A : Set} â†’ â„• â†’ List A â†’ Maybe A
  index n       nil      = Nothing
  index zero    (x , _)  = Just x
  index (suc n) (_ , xs) = index n xs

  -- 4.2.1  High-Order Functions

  dolt3Times : {A : Set} â†’ (A â†’ A) â†’ A â†’ A
  dolt3Times f x = f (f (f x))

  -- 4.2.2  Partial Application
  
  --+ : â„• â†’ â„• â†’ â„•
  
  plus3 : â„• â†’ â„•
  plus3 = _+_ 3

  testPlus3 : plus3 4 â‰¡ 7
  testPlus3 = refl

  -- 4.2.3  Digression: Currying
  
  curry : {A B C : Set} â†’ (A â†’ B â†’ C) â†’ A X B â†’ C
  curry f (x , y) = f x y
  
  uncurry : {A B C : Set} â†’ (A X B â†’ C) â†’ A â†’ B â†’ C
  uncurry f x y = f (x , y)
    
  -- Exercise 4.2

  uncurryCurry : âˆ€ {A B C : Set}(f : A X B â†’ C)(p : A X B) â†’ curry (uncurry f) p â‰¡ f p 
  uncurryCurry f p = {!!}

  curryUncurry : âˆ€ {A B C : Set}(f : A â†’ B â†’ C)(x : A)(y : B) â†’ uncurry (curry f) x y â‰¡ f x y
  curryUncurry f x y = {!!}

  -- 4.2.4  Filter
  
  filter  : {A : Set} â†’ (A â†’ Bool) â†’ List A â†’ List A
  filter p nil      = nil
  filter p (x , xs) with p x
  filter p (x , xs) | True  = x , filter p xs
  filter p (x , xs) | False = filter p xs

  testFilterEven : filter evenb (1 , (2 , (0 , (3 , nil)))) â‰¡ (2 , (0 , nil))
  testFilterEven = refl
  
  countOddMembers : List â„• â†’ â„•
  countOddMembers l = length (filter oddb l)

  testCountOddMembers : countOddMembers (1 , (2 , (3 , nil))) â‰¡ 2
  testCountOddMembers = refl

  -- 4.2.5 
    
  lengthls1 : {A : Set} â†’ List A â†’ Bool
  lengthls1 l = beqNat (length l) 1
  
  testFilter : filter lengthls1 ((1 , (2 , nil)) , ((1 , nil) , nil)) â‰¡ ((1 , nil) , nil)
  testFilter = refl

  testAnonymous : (dolt3Times {â„•} (Î» x â†’ x + x) 2) â‰¡ 16
  testAnonymous = refl

  testFilter2 : filter (Î» x â†’ beqNat (length x) 1) ((1 , (2 , nil)) , ((1 , nil) , nil)) â‰¡ ((1 , nil) , nil)
  testFilter2 = refl

  -- Exercise 4.3 
  
  filterEven7 : List â„• â†’ List â„•
  filterEven7 l = filter (bleNat 7) (filter evenb l)

  testFilterEven7 : filterEven7 (10 , ( 8 , (9 , (4 , (3 , nil))))) â‰¡ (10 , (8 , nil))
  testFilterEven7 = refl

  -- Exercise 4.4
  
  partition : {A : Set} â†’ (A â†’ Bool) â†’ List A â†’ (List A X List A)
  partition p l = {!!}
  
  -- 4.2.6  Map

  map : {A B : Set} â†’ (A â†’ B) â†’ List A â†’ List B
  map _ nil      = nil
  map f (x , xs) = f x , map f xs

  testMap : map plus3 (1 , (2 , nil)) â‰¡ (4 , (5 , nil))
  testMap = refl

  testMap1 : map evenb (1 , (2 , (3 , nil))) â‰¡ (False , (True , (False , nil)))
  testMap1 = refl

  -- Exercise 4.5
  
  distMap : âˆ€ {A B : Set}(f : A â†’ B)(l1 l2 : List A) â†’ map f (l1 âŸ¨++âŸ© l2) â‰¡ map f l1 âŸ¨++âŸ© map f l2
  distMap f nil      l2 = refl
  distMap f (x , l1) l2 = 
    map f ((x , l1) âŸ¨++âŸ© l2) â‰¡âŸ¨ refl âŸ©
    f x , map f (l1 âŸ¨++âŸ© l2) â‰¡âŸ¨ cong (Î» a â†’ f x , a) (distMap f l1 l2) âŸ©
    f x , ((map f l1) âŸ¨++âŸ© (map f l2))
    âˆŽ

  mapRevComm : âˆ€ {A B : Set}(f : A â†’ B)(l : List A) â†’ map f (rev l) â‰¡ rev (map f l)
  mapRevComm f nil     = refl
  mapRevComm f (x , l) = 
    map f (rev (x , l))            â‰¡âŸ¨ refl âŸ©
    map f (snoc x (rev l))         â‰¡âŸ¨ cong (Î» a â†’ map f a) (snocApp x (rev l)) âŸ©
    map f (rev l âŸ¨++âŸ© (x , nil))   â‰¡âŸ¨ distMap f (rev l) (x , nil) âŸ©
    map f (rev l) âŸ¨++âŸ© (f x , nil) â‰¡âŸ¨ sym (snocApp (f x) (map f (rev l))) âŸ©
    snoc (f x) (map f (rev l))     â‰¡âŸ¨ cong (Î» a â†’ snoc (f x) a) (mapRevComm f l) âŸ©
    snoc (f x) (rev (map f l))     â‰¡âŸ¨ refl âŸ©
    rev (map f (x , l))
    âˆŽ 

  -- Exercise 4.6 -- Arrumar no PDF

  concatMap : {A B : Set} â†’  (A â†’ List B) â†’  List A â†’ List B
  concatMap f nil     = nil
  concatMap f (x , l) = f x âŸ¨++âŸ© (concatMap f l)

  testConcatMap : concatMap (Î» n â†’ (n , (n + 1 , (n + 2 , nil)))) (1 , (5 , (10 , nil))) â‰¡ (1 , (2 , (3 , (5 , (6 , (7 , (10 , (11 , (12 , nil)))))))))
  testConcatMap = refl

  mapMaybe : {A B : Set} â†’ (A â†’ B) â†’ Maybe A â†’ Maybe B
  mapMaybe f (Just x) = Just (f x)
  mapMaybe f Nothing  = Nothing

  -- 4.2.7  Fold

  fold : {A B : Set} â†’ (A â†’ B â†’ B) â†’ B â†’ List A â†’ B
  fold f v nil = v
  fold f v (x , xs) = f x (fold f v xs)

  testFold : fold _+_ 0 (1 , (2 , (3 , (4 , nil)))) â‰¡ 10
  testFold = refl

  -- 4.2.8  Functions for Constructing Fuctions

  constFun : {A : Set} â†’ A â†’ â„• â†’ A
  constFun x n = x
  
  ftrue : â„• â†’ Bool
  ftrue = constFun True
  
  testFtrue1 : ftrue 0 â‰¡ True
  testFtrue1 = refl
  
  override : {A : Set} â†’ (â„• â†’ A) â†’ â„• â†’ A â†’ â„• â†’ A
  override f x a y with beqNat x y
  ...| True  = a
  ...| False = f y
  
  fMostlyTrue : â„• â†’ Bool
  fMostlyTrue = override (override ftrue 1 False) 3 False
  
  testFMostlyTrue1 : fMostlyTrue 0 â‰¡ True
  testFMostlyTrue1 = refl
  
  testFMostlyTrue2 : fMostlyTrue 1 â‰¡ False
  testFMostlyTrue2 = refl

  -- Exercise 4.7
  
  overrideExample : âˆ€ (b : Bool) â†’ (override (constFun b) 3 True) 2 â‰¡ b
  overrideExample True  = {!!}
  overrideExample False = {!!}

  -- 4.3   Additional Exercices
  
  -- Exercise 4.8
  
  foldLength : âˆ€ {A : Set} â†’ List A â†’ â„•
  foldLength = fold (Î» _ a â†’ suc a) 0

  theoremLength : âˆ€ {A : Set}(l : List A) â†’ length l â‰¡ foldLength l
  theoremLength nil     = refl
  theoremLength (x , l) = 
    length (x , l)     â‰¡âŸ¨ refl âŸ©
    suc (length l)     â‰¡âŸ¨ cong suc (theoremLength l) âŸ©
    suc (foldLength l)
    âˆŽ

  --mapFold : {A B : Set} â†’ (A â†’ B) â†’ List A â†’ List B
  --mapFold f l = fold (Î» x y â†’ f x , y) nil l

  foldMap : {A B : Set}(f : A â†’ B)(l : List A) â†’ map f l â‰¡ fold (Î» a b â†’ f a , b) nil l
  foldMap f nil     = refl
  foldMap f (x , l) = 
    map f (x , l)                      â‰¡âŸ¨ refl âŸ©
    f x , map f l                      â‰¡âŸ¨ cong (Î» a â†’ f x , a) (foldMap f l) âŸ©
    f x , fold (Î» a b â†’ f a , b) nil l â‰¡âŸ¨ refl âŸ©
    fold (Î» a b â†’ f a , b) nil (x , l)
    âˆŽ

open Polymorphism public

module MoreAgda where

  Eq : {l : Level}(A : Set l) â†’ A â†’ A â†’ Set l
  Eq {l} A x y = _â‰¡_ {l} {A} x y
  
  NListEq : List â„• â†’ List â„• â†’ Set
  NListEq l1 l2 = Eq (List â„•) l1 l2

  Silly : (n m o p : â„•) â†’ Set
  Silly n m o p = n â‰¡ m â†’ t1 â†’ t2
    where
      t1 = NListEq (n , (o , nil)) (n , (p , nil))
      t2 = NListEq (n , (o , nil)) (m , (p , nil))
  
  id : Set â†’ Set
  id x = x â†’ x

  silly1 : âˆ€ (n m o p : â„•) â†’ Silly m o n p -- Perguntar o Rodrigo (nao foi feito o id)
  silly1 m n o p nm rewrite nm = Î» x â†’ x --id

  Hyp : â„• â†’ â„• â†’ Set
  Hyp o p = âˆ€ (q r : â„•) â†’ q â‰¡ r â†’ t q r
    where
      t : â„• â†’ â„• â†’ Set
      t q r = NListEq (q , (o , nil)) (r , (p , nil))

  Silly2 : (m n o p : â„•) â†’ Set
  Silly2 m n o p = n â‰¡ m â†’ Hyp o p â†’ t
    where
      t = NListEq (n , (o , nil)) (m , (p , nil))


  silly2 : âˆ€ (m n o p : â„•) â†’ Silly2 m n o p
  silly2 m n o p nm hyp = hyp n m nm

  NPairEq : (m n o p : â„•) â†’ Set
  NPairEq m n o p = Eq (â„• X â„•) (m , n) (o , p)
  
  Hyp2a : Set
  Hyp2a = âˆ€ (q r : â„•) â†’ NPairEq q q r r â†’ NListEq (q , nil) (r , nil)
  
  Silly2a : â„• â†’ â„• â†’ Set
  Silly2a n m = NPairEq n n m m â†’ Hyp2a â†’ NListEq (n , nil) (m , nil)
  
  silly2a : âˆ€ (n m : â„•) â†’ Silly2a n m
  silly2a .m m refl hyp = refl
  
  SillyEx : Set
  SillyEx = âˆ€ (n : â„•) â†’ evenb n â‰¡ True â†’ oddb (suc n) â‰¡ True
  
  sillyEx : SillyEx â†’ evenb 3 â‰¡ True â†’ oddb 4 â‰¡ True
  sillyEx silly = silly (suc (suc (suc zero)))

  silly3 : âˆ€ (n : â„•) â†’ True â‰¡ beqNat n 5 â†’ beqNat (suc (suc n)) 7 â‰¡ True
  silly3 n = sym
  
  revExercice1 : âˆ€ (l1 l2 : List â„•) â†’ l1 â‰¡ rev l2 â†’ l2 â‰¡ rev l1
  revExercice1 nil      l2 prf = {!!}
  revExercice1 (x , l1) l2 prf = {!!}
